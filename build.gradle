plugins {
    id 'java'
    id 'application'
    id 'org.beryx.jlink' version '2.26.0'
}

java {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
}

def jlinkName = 'ppp'
jlink {
    options.set(['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages'])
    launcher { name = jlinkName }
    imageName.set jlinkName
    imageZip.set(new File(project.buildDir, jlinkName + '.zip'))
    // reactor needs some help to work with modules (javafx)
    mergedModule {
        excludeProvides servicePattern: 'io.micrometer.context.ContextAccessor'
        excludeProvides servicePattern: 'reactor.blockhound.integration.BlockHoundIntegration'
    }
}

tasks.register('buildRelease') { dependsOn('jlinkZip') }

application {
    mainModule.set 'PenguinPlundgePortA2.main'
    mainClass.set 'com.twopercent.main.Main'
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir 'src/'
        }
        resources.srcDir 'res/'
    }
}

def fxVersion = '20.0.1'
// def fxVersion = '11.0.2'

dependencies {
    implementation("org.openjfx:javafx-base:$fxVersion:linux")
    implementation("org.openjfx:javafx-controls:$fxVersion:linux")
    implementation("org.openjfx:javafx-fxml:$fxVersion:linux")
    implementation("org.openjfx:javafx-swing:$fxVersion:linux")
    implementation("org.openjfx:javafx-media:$fxVersion:linux")
    implementation("org.openjfx:javafx-graphics:$fxVersion:linux")

    implementation('io.projectreactor:reactor-core:3.5.5')
    annotationProcessor('org.projectlombok:lombok:1.18.26')
    implementation('org.projectlombok:lombok:1.18.26')
    implementation('org.slf4j:slf4j-api:2.0.4')
    implementation('ch.qos.logback:logback-classic:1.4.7')
}

def extraExports = [
        'javafx.base/com.sun.javafx.collections',
        'javafx.graphics/com.sun.glass.events',
]
def exportTo = application.mainModule.get()

tasks.withType(JavaCompile).configureEach {
    extraExports.each { export ->
        options.compilerArgs += ['--add-exports', "$export=$exportTo"]
    }
}

tasks.withType(JavaExec).configureEach {
    extraExports.each { export ->
        jvmArgs(['--add-exports', "$export=$exportTo"])
    }
}
